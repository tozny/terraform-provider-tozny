# tozny_realm_application_mapper Resource

Resource for provisioning a TozID Application Mapper for mapping data during an authentication protocol flow such as OIDC or SAML.

This resource requires that the account username and password be supplied to the provider either via explicit provider settings or file based credentials.

## Example Usage
```hcl
# Configure the Terraform runtime
terraform {
  required_version = ">= 0.13"
  required_providers {
    tozny = {
      # Pull signed provider binaries from
      # the Terraform hosted registry namespace
      # for Tozny registry.terraform.io/tozny
      source  = "tozny/tozny"
      # Pin Tozny provider version
      version = ">=0.10.1"
    }
  }
}

# Include the Tozny Terraform provider
provider "tozny" {
  api_endpoint = "http://platform.local.tozny.com:8000"
  account_username = "test+${random_string.account_username_salt.result}@tozny.com"
}

# Generate a random string for use in creating
# accounts across environments or executions conflict free
resource "random_string" "account_username_salt" {
  length = 8
  special = false
}

# Generate a random string for use in creating
# realms across environments or executions conflict free
resource "random_string" "random_realm_name" {
  length = 8
  special = false
}

# Local variables for defining where to store and find local Tozny client credentials
locals {
  tozny_client_credentials_filepath = "./tozny_client_credentials.json"
}

# A resource for provisioning a Tozny account using Terraform generated
# credentials that are saved to user specified filepath for reuse upon success.
resource "tozny_account" "autogenerated_tozny_account" {
  autogenerate_account_credentials = true
  persist_credentials_to = "file"
  client_credentials_save_filepath = local.tozny_client_credentials_filepath
}

# A resource for provisioning a TozID Realm
# using local file based credentials for a Tozny Client with permissions to manage Realms.
resource "tozny_realm" "my_organizations_realm" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = random_string.random_realm_name.result
  sovereign_name = "Administrator"
}

# A resource for provisioning a token that can be used to register Tozny clients
# such as a realm broker identity
resource "tozny_client_registration_token" "realm_registration_token" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  name = "${tozny_realm.my_organizations_realm.realm_name}DefaultClientRegistrationToken"
  allowed_registration_client_types = ["general", "identity", "broker"]
  enabled = true
  one_time_use = false
}

# A resource for provisioning an identity that can be used to delegate authority for
# realm activities such as email recovery
resource "tozny_realm_broker_identity" "broker_identity" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  client_registration_token = tozny_client_registration_token.realm_registration_token.token
  realm_name = tozny_realm.my_organizations_realm.realm_name
  name = "broker${tozny_realm.my_organizations_realm.realm_name}"
  broker_identity_credentials_save_filepath = "./${tozny_realm.my_organizations_realm.realm_name}_broker_identity_credentials.json"
}

# A resource for delegating authority to another Tozny client
# (either hosted by Tozny or some other third party) to broker realm
# activities (such as email recovery).
resource "tozny_realm_broker_delegation" "allow_tozny_hosted_brokering_policy" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
    tozny_realm_broker_identity.broker_identity,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_broker_identity_credentials_filepath = "./${tozny_realm.my_organizations_realm.realm_name}_broker_identity_credentials.json"
  use_tozny_hosted_broker = true
}

# A resource for creating an OIDC based realm application
resource "tozny_realm_application" "jenkins_oidc_application" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
    tozny_realm.my_organizations_realm,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  client_id = "jenkins-oid-app"
  name = "Jenkins"
  active = true
  protocol = "openid-connect"
  oidc_settings {
    allowed_origins = [ "https://example.frontend.com" ]
    root_url = "https://jenkins.acme.com"
  }
}

# A resource for creating a SAML based realm application
resource "tozny_realm_application" "aws_saml_application" {
  # Block on and use client credentials generated from the provisioned account
  depends_on = [
    tozny_account.autogenerated_tozny_account,
    tozny_realm.my_organizations_realm,
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  client_id = "aws-saml-app"
  name = "AWS"
  active = true
  protocol = "saml"
  saml_settings {
    allowed_origins = [ "https://example.frontend.com" ]
    default_endpoint = "https://samuel/saml/iam"
    include_authn_statement = true
    include_one_time_use_condition = true
    sign_documents = true
    sign_assertions = true
    client_signature_required = true
    force_post_binding = true
    force_name_id_format = true
    name_id_format = "name_id_format"
    idp_initiated_sso_url_name = "sso_url_name"
    assertion_consumer_service_post_binding_url = "post_binding_url"
  }
}

# A resource for creating an application oidc mapper for identity policy attribute
resource "tozny_realm_application_mapper" "oidc_client_policy_mapper" {
  depends_on = [
    tozny_realm_application.aws_saml_application
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  application_id = tozny_realm_application.jenkins_oidc_application.application_id
  name = "Client Policy"
  protocol = "openid-connect"
  mapper_type = "oidc-user-attribute-mapper"
  add_to_user_info = true
  add_to_id_token = true
  add_to_access_token = true
  multivalued = false
  aggregate_attribute_values = false
  user_attribute = "policy"
  claim_json_type = "String"
  token_claim_name = "policy"
}

# A resource for creating an application oidc mapper for identity group membership
resource "tozny_realm_application_mapper" "oidc_group_memebership_mapper" {
  depends_on = [
    tozny_realm_application.aws_saml_application
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  application_id = tozny_realm_application.jenkins_oidc_application.application_id
  name = "group-membership"
  protocol = "openid-connect"
  mapper_type = "oidc-group-membership-mapper"
  add_to_user_info = true
  add_to_id_token = true
  add_to_access_token = true
  claim_json_type = "String"
  token_claim_name = "group-membership"
  full_group_path = true
}

# A resource for creating an application saml mapper for identity name attribute
resource "tozny_realm_application_mapper" "saml_name_mapper" {
  depends_on = [
    tozny_realm_application.aws_saml_application
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  application_id = tozny_realm_application.aws_saml_application.application_id
  name = "name"
  protocol = "saml"
  mapper_type = "saml-user-property-mapper"
  property = "Username"
  friendly_name = "Username"
  saml_attribute_name = "name"
  saml_attribute_name_format = "Basic"
}

# A resource for creating an application saml mapper for identity roles attribute
resource "tozny_realm_application_mapper" "saml_roles_mapper" {
  depends_on = [
    tozny_realm_application.aws_saml_application
  ]
  client_credentials_filepath = local.tozny_client_credentials_filepath
  realm_name = tozny_realm.my_organizations_realm.realm_name
  application_id = tozny_realm_application.aws_saml_application.application_id
  name = "roles"
  protocol = "saml"
  mapper_type = "saml-role-list-mapper"
  role_attribute_name = "roles"
  friendly_name = "Roles"
  saml_attribute_name_format = "Basic"
  single_role_attribute = true
}
```

## Argument Reference

### Top-Level Arguments

* `client_credentials_filepath` - (Optional) The filepath to Tozny client credentials for the Terraform provider to use when provisioning this resource. Omit if using `client_credentials_config`.
* `client_credentials_config` - (Optional) A JSON string containing Tozny client credentials for the provider to use when provisioning this resource. Omit if using `client_credentials_filepath`.
* `application_mapper_id` - (Computed) Service defined unique identifier for the application mapper.
* `application_id` - (Required) ID of the Application the Mapper is associated with.
* `realm_name` - (Required) The name of the Realm to provision the Application Mapper in.
* `name` - (Required) User defined name for the application mapper.
* `protocol` - (Required) The identity protocol that this mapper will be applied to flows of. Valid values are `openid-connect`, `saml`.
* `mapper_type` - (Required) The category of data this mapper is applied to. Valid values are `oidc-user-session-note-mapper`, `oidc-user-attribute-mapper`, `oidc-group-membership-mapper`, `saml-role-list-mapper`, `saml-user-property-mapper`.
* `user_session_note` - (Optional) Name of stored user session note within the UserSessionModel.note map.
* `user_attribute` - (Optional) Name of stored user attribute within the UserModel.attribute map.
* `full_group_path` - (Optional) If true, full path format will be used when group membership is mapped, if false, only single-level group paths are used.
* `token_claim_name` - (Optional) Name of the claim to insert into the token. This can be a fully qualified name like 'address.street'. In this case, a nested json object will be created. To prevent nesting and use dot literally, escape the dot with backslash `(\.)`.
* `claim_json_type` - (Optional) JSON type that should be used to populate the json claim in the token. Valid `String`, `int`, `bool`, `long`.
* `add_to_id_token` - (Optional) Indicates if the claim should be added to the id token. Defaults to false.
* `add_to_access_token` - (Optional) Indicates if the claim should be added to the access token. Defaults to false.
* `add_to_user_info` - (Optional) Indicates if the claim should be added to the user info. Defaults to false.
* `multivalued` - (Optional) Indicates if attribute supports multiple values. If true, then the list of all values of this attribute will be set as claim. If false, then just first value will be set as claim.
* `aggregate_attribute_values` - (Optional) Indicates if attribute values should be aggregated with the group attributes. If using OpenID Connect mapper the multivalued option needs to be enabled too in order to get all the values. Duplicated values are discarded and the order of values is not guaranteed with this option.
* `saml_attribute_name` - (Optional) Name of the SAML attribute that should be used for mapping an identities name.
* `saml_attribute_name_format` - (Optional) Format to use for the name attribute for the SAML protocol, valid values are `Basic` `URI Reference` or `Unspecified`.
* `friendly_name` - (Optional) Standard SAML attribute setting. An optional, more human-readable form of the attribute's name that can be provided if the actual attribute name is cryptic.
* `role_attribute_name` - (Optional) Name of the SAML attribute you want to put your roles into. i.e. 'Role', 'memberOf'.
* `property` - (Optional) Name of the property method in the UserModel interface. For example, a value of 'email' would reference the UserModel.getEmail() method.
* `single_role_attribute` - (Optional) If true, all roles will be stored under one attribute with multiple attribute values.
* `application_mapper_id` - (Computed) Server defined unique identifier for the Application Mapper.

## Attribute Reference

* `id` - Unique ID of the provisioned application mapper.
